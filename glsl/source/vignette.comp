#version 450

// 调整图像的对比度

layout (local_size_x = 16, local_size_y = 16) in;// gl_WorkGroupSize

layout (binding = 0, rgba8) uniform readonly image2D inTex;
layout (binding = 1, rgba8) uniform image2D outTex;

layout (std140, binding = 2) uniform UBO {
    float vignetteCenterX;
    float vignetteCenterY;
    float vignetteColorX;
    float vignetteColorY;
    float vignetteColorZ;
    float vignetteStart;
    float vignetteEnd;
} ubo;

void main(){
    ivec2 uv = ivec2(gl_GlobalInvocationID.xy);
    ivec2 size = imageSize(inTex);    
    if(uv.x >= size.x || uv.y >= size.y){
        return;
    }   
    vec2 suv = (vec2(uv)+vec2(0.5f))/vec2(size); 
    vec4 color = imageLoad(inTex,uv); 
    vec2 vignetteCenter = vec2(ubo.vignetteCenterX,ubo.vignetteCenterY);
    vec3 vignetteColor = vec3(ubo.vignetteColorX,ubo.vignetteColorY,ubo.vignetteColorZ);
    float d = distance(suv, vignetteCenter);
    float percent = smoothstep(ubo.vignetteStart, ubo.vignetteEnd, d);
    vec4 result = vec4(mix(color.rgb, vignetteColor, percent), color.a);
    imageStore(outTex,uv,result);
}