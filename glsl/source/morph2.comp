#version 450

// #define IS_SHARED 1

layout (local_size_x = 16, local_size_y = 16) in;// gl_WorkGroupSize

#if CHANNEL_RGBA
layout (binding = 0, rgba8) uniform readonly image2D inTex;
layout (binding = 1, rgba8) uniform image2D outTex;
#elif CHANNEL_R8
layout (binding = 0, r8) uniform readonly image2D inTex;
layout (binding = 1, r8) uniform image2D outTex;
#endif

layout (binding = 2) uniform UBO {
	int ksize;	    
} ubo;

#if EROSION 
    #define OPERATE min
    #if CHANNEL_RGBA
        #define INIT_VUL vec4(1.0)
    #elif CHANNEL_R8
        #define INIT_VUL 1.0f
    #endif
#endif

#if DILATION 
    #define OPERATE max
    #if CHANNEL_RGBA
        #define INIT_VUL vec4(0.0)
    #elif CHANNEL_R8
        #define INIT_VUL 0.0f
    #endif
#endif

#if IS_SHARED

// 限定最大核为32
#if CHANNEL_RGBA
    shared vec4 row_shared[16*3][16];
#elif CHANNEL_R8
    shared float row_shared[16*3][16];
#endif

void main(){
    ivec2 uv = ivec2(gl_GlobalInvocationID.xy);
    ivec2 size = imageSize(inTex);
    if(uv.x >= size.x || uv.y >= size.y){
        return;
    } 
    ivec2 locId = ivec2(gl_LocalInvocationID.xy);
    for(int i = 0; i < 3; i++){
        uint gIdy = max(0,min(uv.y+(i-1)*16,size.y-1));
        #if CHANNEL_RGBA
            row_shared[locId.y + i*16][locId.x] = imageLoad(inTex,ivec2(uv.x,gIdy)); 
        #elif CHANNEL_R8
            row_shared[locId.y + i*16][locId.x] = imageLoad(inTex,ivec2(uv.x,gIdy)).r; 
        #endif     
    }
    memoryBarrierShared();
	barrier();
    #if CHANNEL_RGBA
        vec4 result = INIT_VUL;
    #elif CHANNEL_R8
        float result = INIT_VUL;
    #endif
    for(int i =0; i < ubo.ksize; i++){
        int iy = locId.y - ubo.ksize/2 + i;
        #if CHANNEL_RGBA
            vec4 fr = row_shared[16 + iy][locId.x];
            result = OPERATE(fr,result);
        #elif CHANNEL_R8
            float fr = row_shared[16 + iy][locId.x];
            result = OPERATE(fr,result);
        #endif
    }
    imageStore(outTex, uv, vec4(result)); 
}

#else

void main(){
    ivec2 uv = ivec2(gl_GlobalInvocationID.xy);
    ivec2 size = imageSize(inTex);
    if(uv.x >= size.x || uv.y >= size.y){
        return;
    } 
    #if CHANNEL_RGBA
        vec4 result = INIT_VUL;
    #elif CHANNEL_R8
        float result = INIT_VUL;
    #endif
    for(int i = 0; i< ubo.ksize; ++i){
        int y = uv.y-ubo.ksize/2+i;
        y = max(0,min(y,size.y-1));
        #if CHANNEL_RGBA
            vec4 r = imageLoad(inTex,ivec2(uv.x,y));
            result = OPERATE(result,r);
        #elif CHANNEL_R8
            float r = imageLoad(inTex,ivec2(uv.x,y)).r;
            result = OPERATE(result,r);
        #endif        
    }
    imageStore(outTex, uv, vec4(result));     
}

#endif

