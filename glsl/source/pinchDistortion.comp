#version 450

layout (local_size_x = 16, local_size_y = 16) in;// gl_WorkGroupSize

layout (binding = 0) uniform sampler2D inSampler;
layout (binding = 1, rgba8) uniform image2D outTex;

layout (std140, binding = 2) uniform UBO {
    float aspectRatio;
    float centerX;
    float centerY;
    float radius;
    float scale;
} ubo;

void main(){
    ivec2 uv = ivec2(gl_GlobalInvocationID.xy);
    ivec2 size = imageSize(outTex);    
    if(uv.x >= size.x || uv.y >= size.y){
        return;
    }   
    vec2 center = vec2(ubo.centerX,ubo.centerY);
    float x = (uv.x+0.5)/size.x;
    float y = (uv.y+0.5)/size.y;
    vec2 coord = vec2(x,((y-ubo.centerY)*ubo.aspectRatio)+ubo.centerY);  
    float dist = distance(center,coord);
    vec2 tcoord = vec2(x,y);
    if(dist < ubo.radius){
        tcoord = tcoord - center;        
        float percent = 1.0 + ((0.5 - dist) / 0.5) * ubo.scale;
        percent = percent* percent;
        tcoord = tcoord * percent;
        tcoord = tcoord + center;
    }
    vec4 inColor = textureLod(inSampler, tcoord,0);  
    imageStore(outTex,uv,inColor);
}