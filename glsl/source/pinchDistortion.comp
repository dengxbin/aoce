#version 450

layout (local_size_x = 16, local_size_y = 16) in;// gl_WorkGroupSize

layout (binding = 0) uniform sampler2D inSampler;
layout (binding = 1, rgba8) uniform image2D outTex;

layout (std140, binding = 2) uniform UBO {
    float aspectRatio;
    float centerX;
    float centerY;
    float radius;
    float scale;
} ubo;

void main(){
    ivec2 uv = ivec2(gl_GlobalInvocationID.xy);
    ivec2 size = imageSize(outTex);    
    if(uv.x >= size.x || uv.y >= size.y){
        return;
    }   
    vec2 center = vec2(ubo.centerX,ubo.centerY);
    vec2 suv = (vec2(uv)+vec2(0.5f))/vec2(size);      
    vec2 coord = vec2(suv.x,(suv.y-0.5)*ubo.aspectRatio+0.5);  
    float dist = distance(center,coord);
    coord = suv;
    if(dist < ubo.radius){
        // 圆心坐标系
        coord = coord - center;  
        // 圆心距离-放大坐标倍数      
        float percent = 1.0 + ((0.5 - dist) / 0.5) * ubo.scale;
        percent = percent * percent;
        coord = coord * percent;
        coord = coord + center;
    }
    vec4 inColor = textureLod(inSampler, coord,0);  
    imageStore(outTex,uv,inColor);
}