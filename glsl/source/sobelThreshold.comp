#version 450

layout (local_size_x = 16, local_size_y = 16) in;// gl_WorkGroupSize

#if CHANNEL_RGBA
layout (binding = 0, rgba8) uniform readonly image2D inTex;
#elif CHANNEL_R8
layout (binding = 0, r8) uniform readonly image2D inTex;
#endif

layout (binding = 1, r8) uniform image2D outTex;

layout (binding = 2) uniform UBO {
	float edgeStrength;
    float threshold;
} ubo;

const float horizontKernel[9] = {-1.0f,-2.0f,-1.0f,0.0f,0.0f,0.0f,1.0f,2.0f,1.0f};
const float verticalKernel[9] = {-1.0f,0.0f,1.0f,-2.0f,0.0f,2.0f,-1.0f,0.0f,1.0f};

void main(){
    ivec2 uv = ivec2(gl_GlobalInvocationID.xy);
    ivec2 size = imageSize(outTex);    
    if(uv.x >= size.x || uv.y >= size.y){
        return;
    }
    float vertical = 0.0f;
    float horizont = 0.0f;    
    for(int i = 0; i< 3; ++i){
        for(int j= 0; j< 3; ++j){
            int x = uv.x - 1 + j;
            int y = uv.y - 1 + i;
            // REPLICATE border
            x = max(0,min(x,size.x-1));
            y = max(0,min(y,size.y-1));
            int index = j+i*3;
            #if CHANNEL_RGBA 
                float value = imageLoad(inTex,ivec2(x,y)).a;
            #elif CHANNEL_R8
                float value = imageLoad(inTex,ivec2(x,y)).r;
            #endif
            vertical += value * verticalKernel[index];
            horizont += value * horizontKernel[index];
        }
    }
    float mag = length(vec2(horizont, vertical)) * ubo.edgeStrength;
    mag = step(ubo.threshold,mag);
#if SKETCH
    mag = 1.0f - mag;
#endif  
    imageStore(outTex, uv, vec4(mag));    
}